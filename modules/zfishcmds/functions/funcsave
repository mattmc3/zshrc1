### funcsave
# save a function to $funcdir for lazy loading

local funcdir
zstyle -s ':zebrafish:modules:zfishcmds:' 'funcdir' funcdir ||
  funcdir=${ZDOTDIR:-$HOME/.config/zsh}/functions

# check args
if [[ -z "$1" ]]; then
  echo >&2 "funcsave: Expected function name argument" && return 1
elif ! typeset -f "$1" > /dev/null; then
  echo >&2 "funcsave: Unknown function '$1'" && return 1
fi

# make sure the dir exists
mkdir -p $funcdir

# make sure the function is loaded in case it's already lazy
autoload +X "$1" > /dev/null

# remove first/last lines (ie: 'function foo {' and '}') and de-indent one level
type -f "$1" | awk 'NR>2 {print prev} {gsub(/^\t/, "", $0); prev=$0}' >| "$funcdir/$1"
